#State-space surplus production model with lognormal errors
#I index of abund
#P = B/K

#############################################################################
model {

#Observation equation
        for(i in 1:21) {
				Imean[i] <- log(q1*K*P[i]);
				I1[i] ~ dlnorm(Imean[i],itau2);
                 B[i] <- K*P[i] 
                 }
        for(j in 22:42){
				Imean[j] <- log(q1*K*P[j]);
				Imean1[j-21] <- log(q2*K*P[j]);
				I1[j] ~ dlnorm(Imean[j],itau2);
               I2[j-21] ~ dlnorm(Imean1[j-21],igam2);
               
                B[j] <- K*P[j] 
                
         }
        
#Process equation
	#P[1]
	
	Pmean[1] <- log(P0)	;
	P[1] ~ dlnorm(Pmean[1],isigma2)
	P.res[1] <- log(P[1])-Pmean[1]
	
	#P[2:N]
	for (i in 2:N) {
		    Pmean[i] <- log(max(P[i-1] + r*P[i-1]*(1-P[i-1]) - k*C[i-1],0.01));
		    P[i] ~ dlnorm(Pmean[i],isigma2);
		    P.res[i] <- log(P[i]) - Pmean[i]
		    }

#####Prior on r######

		r~dnorm(r.a,r.b);
				
#####Prior on k######
		i.k.b<-1/k.b
		k ~ dlnorm(k.a,i.k.b);
		K <- 1/k;	
		
#Prior on P0
P0 ~dunif(P0.a,P0.b)
		
#####Prior on Q#####
		
		q1 ~dunif(q.a,q.b)
		q2 ~dunif(q.a,q.b)		

#######Priors on isigma2 and itau2#####
	
		sd.p ~ dunif(0.01,7)
		sd.o ~ dunif(0.01,7)
		sd.o2 ~ dunif(0.01,7)
		
		isigma2 <- pow(sd.p,-2)
		itau2    <- pow(sd.o,-2)
		igam2    <- pow(sd.o2,-2)
				
}